buildscript {
    ext {
        kotlin_version = '1.1.3-2'
    }

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.1"
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-jpa'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

version '1.0-SNAPSHOT'
mainClassName = 'com.example.helloworld.HelloWorldApplication'
sourceCompatibility = 1.8

// Set our project variables
project.ext {
    dropwizard_version = '1.1.2'
}

repositories {
    mavenCentral()
}

dependencies {
    compile (
            "io.dropwizard:dropwizard-core:$dropwizard_version",
            "io.dropwizard:dropwizard-auth:$dropwizard_version",
            "io.dropwizard:dropwizard-assets:$dropwizard_version",
            "io.dropwizard:dropwizard-http2:$dropwizard_version",
            "io.dropwizard:dropwizard-hibernate:$dropwizard_version",
            "io.dropwizard:dropwizard-migrations:$dropwizard_version",
            "io.dropwizard:dropwizard-views-freemarker:$dropwizard_version",
            "io.dropwizard:dropwizard-views-mustache:$dropwizard_version",
            "io.dropwizard:dropwizard-metrics-graphite:$dropwizard_version",
            "com.h2database:h2:1.4.196",
            "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version",
            "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    )
    testCompile (
            "io.dropwizard:dropwizard-testing:$dropwizard_version",
            "org.mockito:mockito-core:2.8.47",
            "org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-inmemory:2.25.1",
            "org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:2.25.1"
    )
}

// Configure the shadow jar task
shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}
jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}
